const HDLD_KS23="k23";

class HD_KS23:HDWeapon{
	default{
		+hdweapon.fitsinbackpack
		scale 0.60;
		weapon.selectionorder 50;
		weapon.slotnumber 4;
		weapon.kickback 30;
		weapon.bobrangex 0.21;
		weapon.bobrangey 0.86;
		weapon.bobstyle "normal";
		obituary "%k made %o yee their last haw.";
		inventory.pickupmessage "You got the lever action rifle!";
		tag "Sawyer LR37 ''Big Boy'' Hunting Carbine";
		//hdweapon.refid HDLD_LEVERRIFLE_355;
		hdweapon.barrelsize 28,0.6,2;
		inventory.maxamount 3;
	}
	override double gunmass(){
		return 12;
	}
	override double weaponbulk(){
		return 110+weaponstatus[KS23S_MAG]*ENC_50AM_LOADED;
	}
	override string,double getpickupsprite(){
		return "LVGPZ0",1.;
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel==1){
			sb.drawimage("50AMW0",(-44,-10),sb.DI_SCREEN_CENTER_BOTTOM,scale:(2.1,2.1));
			sb.drawnum(hpl.countinv("HD50AM_Ammo"),-46,-8,
				basestatusbar.DI_SCREEN_CENTER_BOTTOM,font.CR_BLACK
			);
		}
		if(hdw.weaponstatus[0]&KS23F_SELECTFIRE)sb.drawwepcounter(hdw.weaponstatus[0]&KS23F_FIREMODE,
			-22,-10,"RBRSA3A7","STFULAUT"
		);
		sb.drawwepnum(hdw.weaponstatus[KS23S_MAG],10);
		if(hdw.weaponstatus[KS23S_CHAMBER]==2)sb.drawwepdot(-16,-10,(3,1));
	}
	override string gethelptext(){
		return
		WEPHELP_FIRESHOOT
		..WEPHELP_RELOADRELOAD
		..WEPHELP_UNLOADUNLOAD
		;
	}
	override void DrawSightPicture(
		HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl,
		bool sightbob,vector2 bob,double fov,bool scopeview,actor hpc,string whichdot
	){
		int cx,cy,cw,ch;
		[cx,cy,cw,ch]=screen.GetClipRect();
		sb.SetClipRect(
			-16+bob.x,-4+bob.y,32,16,
			sb.DI_SCREEN_CENTER
		);
		vector2 bobb=bob*3;
		bobb.y=clamp(bobb.y,-8,8);
		sb.drawimage(
			"frntsite",(0,0)+bobb,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			alpha:0.9
		);
		sb.SetClipRect(cx,cy,cw,ch);
		sb.drawimage(
			"backsite",(0,0)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP
		);
	}
	override void DropOneAmmo(int amt){
		if(owner){
			amt=clamp(amt,1,10);
			if(owner.countinv("HD50AM_Ammo"))owner.A_DropInventory("HD50AM_Ammo",amt*10);
		}
	}
	
	action void A_SetAltHold(bool which){
		if(which)invoker.weaponstatus[0]|=KS23F_ALTHOLDING;
		else invoker.weaponstatus[0]&=~KS23F_ALTHOLDING;
	}
	
	states{
	select0:
		K230 B 0;
		goto select0small;
	deselect0:
		K230 B 0;
		goto deselect0small;

	ready:
		K230 A 0;
		K230 A 0 A_JumpIf(invoker.weaponstatus[KS23S_HAMMER]<2,2);
		K230 B 0;
		---- # 1{
			A_WeaponReady(WRF_ALL);
			A_SetAltHold(false);
		}goto readyend;
	//user3:
		//---- A 0 A_MagManager("HD9mMag15");
		//goto ready;
	//user2:
	altfire:
		---- A 0 A_JumpIf(invoker.weaponstatus[0]&KS23F_ALTHOLDING,"nope");
		K230 DE 3; //A_WeaponBusy();//offset(0,34)
		K230 F 3 A_SetAltHold(true);//offset(4,38)
		K230 B 0 A_Refire("chamber");
		K230 ED 3;
		goto ready;
	althold:
		K230 H 1 A_WeaponReady(WRF_NOFIRE);
		K230 H 1{
			A_ClearRefire();
			bool chempty=invoker.weaponstatus[KS23S_CHAMBER]<1;
			if(pressingunload()){
				if(chempty){
					return resolvestate("altholdend");
				}else{
					invoker.weaponstatus[0]|=KS23F_JUSTUNLOAD;
					return resolvestate("loadchamber");
				}
			}else if(pressingreload()){
				if(
					!chempty
				){
					invoker.weaponstatus[0]|=KS23F_JUSTUNLOAD;
					return resolvestate("loadchamber");
				}else if(
					countinv("HD50AM_Ammo")
				){
					invoker.weaponstatus[0]&=~KS23F_JUSTUNLOAD;
					return resolvestate("loadchamber");
				}
			}
			if(pressingaltfire())return resolvestate("althold");
			return resolvestate("altholdend");
		}
	altholdend:
		K230 F 3 A_StartSound("weapons/levergun/cock2",8);
		K230 ED 3 A_WeaponReady(WRF_NOFIRE);
		K230 A 1 {
			A_WeaponReady(WRF_NOFIRE);
		}
		goto ready;
	loadchamber:
		K230 F 1 offset(2,36) A_ClearRefire();
		K230 F 1 offset(3,38);
		K230 F 1 offset(5,42);
		K230 F 1 offset(8,48) A_StartSound("weapons/pocket",9);
		K230 F 1 offset(9,52) A_MuzzleClimb(frandom(-0.2,0.2),0.2,frandom(-0.2,0.2),0.2,frandom(-0.2,0.2),0.2);
		K230 F 2 offset(8,60);
		K230 F 2 offset(7,72);
		TNT1 A 18 A_StartSound("weapons/pocket",9);
		TNT1 A 4{
			A_StartSound("weapons/bossload",8,volume:0.7);
			if(invoker.weaponstatus[0]&KS23F_JUSTUNLOAD){
				int chm=invoker.weaponstatus[KS23S_CHAMBER];
				invoker.weaponstatus[KS23S_CHAMBER]=0;
				if(chm<2||A_JumpIfInventory("HD50AM_Ammo",0,"null")){
					class<actor> whatkind=chm==2?"HDLoose50AM":"HDSpent50AM"; //just use 355 if fuck
					actor rrr=spawn(whatkind,pos+(cos(angle)*10,sin(angle)*10,height-12),ALLOW_REPLACE);
					rrr.angle=angle;rrr.A_ChangeVelocity(1,2,1,CVF_RELATIVE);
				}else HDF.Give(self,"HD50AM_Ammo",1);
			}else{
				A_TakeInventory("HD50AM_Ammo",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[KS23S_CHAMBER]=2;
			}
		} 
		K230 F 2 offset(7,72);
		K230 F 2 offset(8,60);
		K230 F 1 offset(7,52);
		K230 F 1 offset(5,42);
		K230 F 1 offset(3,38);
		K230 F 1 offset(2,35);
		goto althold;
		
	chamber:
		K230 G 1 {
			if(
				invoker.weaponstatus[KS23S_CHAMBER]>2
			){
				invoker.weaponstatus[KS23S_CHAMBER]+=2;
				A_MuzzleClimb(
					-frandom(0.6,2.3),-frandom(0.6,2.3),
					-frandom(0.6,1.3),-frandom(0.6,1.3),
					-frandom(0.6,1.3),-frandom(0.6,1.3)
				);
			}else A_StartSound("weapons/levergun/cock1",8);
		}
		K230 H 1 {
			//eject
			
			invoker.weaponstatus[KS23S_HAMMER]=0;
			
			int chm=invoker.weaponstatus[KS23S_CHAMBER];
			if(chm>1){  
				A_SpawnItemEx(
					"HDLoose50AM",cos(pitch)*8,1,height-7-sin(pitch)*8,
					cos(pitch)*cos(angle-80)*4+vel.x,
					cos(pitch)*sin(angle-80)*4+vel.y,
					-sin(pitch)*4+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}else if(chm==1){
				A_SpawnItemEx(
					"HDSpent50AM",cos(pitch)*8,1,height-7-sin(pitch)*8,
					cos(pitch)*cos(angle-80)*6+vel.x,
					cos(pitch)*sin(angle-80)*6+vel.y,
					-sin(pitch)*6+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
			//cycle new
			if(invoker.weaponstatus[KS23S_MAG]>0){  
				invoker.weaponstatus[KS23S_CHAMBER]=2;
				invoker.weaponstatus[KS23S_MAG]--;
			}else invoker.weaponstatus[KS23S_CHAMBER]=0;
		}
		K230 H 8 A_WeaponReady(WRF_NOFIRE);
		K230 H 0 A_Refire("althold");
		goto altholdend;
	hold:
		---- A 0{
			bool paf=pressingaltfire();
			if(
				paf&&!(invoker.weaponstatus[0]&KS23F_ALTHOLDING)
			)setweaponstate("altfire");
			else if(!paf)invoker.weaponstatus[0]&=~KS23F_ALTHOLDING;
		}
		---- A 1 A_WeaponReady(WRF_NONE);
		---- A 0 A_Refire();
		goto ready;
	fire:
		K230 B 0{
		if(invoker.weaponstatus[KS23S_HAMMER]<3)A_StartSound("weapons/rifleclick2",1,CHANF_OVERLAP);
		invoker.weaponstatus[KS23S_HAMMER]=4;
			invoker.weaponstatus[0]&=~KS23F_JUSTUNLOAD;
			if(invoker.weaponstatus[KS23S_CHAMBER]==2)setweaponstate("shoot");
		}
		---- B 1 A_WeaponReady(WRF_NONE);
		---- B 0 A_Refire();
		goto ready;
	shoot:
		#### B 1{
			if(invoker.weaponstatus[KS23S_CHAMBER]==2)A_GunFlash();
		}
		#### D 2{
			A_MuzzleClimb(-frandom(1.4,2.2),-frandom(0.4,1.6));
		}
		K230 C 1;
		K230 B 1 A_WeaponReady();
		---- B 1 A_Refire();
		goto ready;
	flash:
		LVGF A 0;
		---- A 1 bright{
			HDFlashAlpha(64);
			A_Light1();
			let bbb=HDBulletActor.FireBullet(self,"HDB_50AM",speedfactor:1.5);
			if(
				frandom(0,ceilingz-floorz)<bbb.speed*0.3
			)A_AlertMonsters(256);

			invoker.weaponstatus[KS23S_CHAMBER]=1;
			A_ZoomRecoil(0.995);
			A_MuzzleClimb(-frandom(0.4,1.2),-frandom(0.4,1.6));
		}
		---- A 0 A_StartSound("weapons/levergun/fire1",CHAN_WEAPON);
		---- A 0 A_Light0();
		stop;

	reload:
		---- A 1 offset(0,34);
		---- A 1 offset(2,36);
		---- A 1 offset(4,40);
		---- A 2 offset(8,42){
			A_StartSound("weapons/rifleclick2",8,CHANF_OVERLAP,0.9,pitch:0.95);
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		}
		---- A 4 offset(14,46){
			A_StartSound("weapons/rifleload",8,CHANF_OVERLAP);
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		}
		---- A 0{
			int mg=invoker.weaponstatus[KS23S_MAG];
			if(mg==10)setweaponstate("reloaddone");
		}
	loadhand:
		---- A 0 A_JumpIfInventory("HD50AM_Ammo",1,"loadhandloop");
		goto reloaddone;
	loadhandloop:
		---- A 4{
			int hnd=min(
				countinv("HD50AM_Ammo"),1,
				10-invoker.weaponstatus[KS23S_MAG]
			);
			if(hnd<1){
				setweaponstate("reloaddone");
				return;
			}else{
				A_TakeInventory("HD50AM_Ammo",hnd,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[KS23S_HAND]=hnd;
				A_StartSound("weapons/pocket",9);
			}
		}
	loadone:
		---- A 2 offset(16,50) A_JumpIf(invoker.weaponstatus[KS23S_HAND]<1,"loadhandnext");
		---- A 4 offset(14,46){
			invoker.weaponstatus[KS23S_HAND]--;
			invoker.weaponstatus[KS23S_MAG]++;
			A_StartSound("weapons/rifleclick2",8);
		}loop;
	loadhandnext:
		---- A 4 offset(16,48){
			if(
				PressingReload()||
				PressingFire()||
				PressingAltFire()||
				PressingZoom()||
				!countinv("HD50AM_Ammo")	//don't strip clips automatically
			)setweaponstate("reloaddone");
			else A_StartSound("weapons/pocket",9);
		}goto loadhandloop;
	reloaddone:
		---- A 1 offset(4,40);
		---- A 1 offset(2,36);
		---- A 1 offset(0,34);
		goto nope;
	unload:
		---- A 1 offset(0,34);
		---- A 1 offset(2,36);
		---- A 1 offset(4,40);
		---- A 2 offset(8,42){
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
			A_StartSound("weapons/rifleclick2",8);
		}
		---- A 4 offset (14,46){
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
			A_StartSound("weapons/rifleload",8);
		}
	unloadloop:
		---- A 4 offset(3,41){
			if(invoker.weaponstatus[KS23S_MAG]<1)setweaponstate("unloaddone");
			else{
				A_StartSound("weapons/rifleclick2",8);
				invoker.weaponstatus[KS23S_MAG]--;
				if(A_JumpIfInventory("HD50AM_Ammo",0,"null")){
					A_SpawnItemEx(
						"HDLoose50AM",cos(pitch)*8,0,height-7-sin(pitch)*8,
						cos(pitch)*cos(angle-40)*1+vel.x,
						cos(pitch)*sin(angle-40)*1+vel.y,
						-sin(pitch)*1+vel.z,
						0,SXF_ABSOLUTEMOMENTUM|
						SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
					);
				}else A_GiveInventory("HD50AM_Ammo",1);
			}
		}
		---- A 2 offset(2,42);
		---- A 0{
			if(
				PressingReload()||
				PressingFire()||
				PressingAltFire()||
				PressingZoom()
			)setweaponstate("unloaddone");
		}loop;
	unloaddone:
		---- A 2 offset(2,42);
		---- A 3 offset(3,41);
		---- A 1 offset(4,40) A_StartSound("weapons/rifleclick",8);
		---- A 1 offset(2,36);
		---- A 1 offset(0,34);
		goto ready;


	spawn:
		LVGP Z -1 nodelay{
		}stop;
	}
	override void initializewepstats(bool idfa){
		weaponstatus[KS23S_MAG]=10;
		weaponstatus[KS23S_CHAMBER]=2;
	}

}
enum ks23status{
	KS23F_SELECTFIRE=1,
	KS23F_FIREMODE=2,
	KS23F_JUSTUNLOAD=4,
	KS23F_ALTHOLDING=8,

	KS23S_FLAGS=0,
	KS23S_MAG=1,
	KS23S_CHAMBER=2, //0 empty, 1 spent, 2 loaded
	KS23S_HAMMER=3, //0 down, 1 up
	KS23S_HAND=4,
};