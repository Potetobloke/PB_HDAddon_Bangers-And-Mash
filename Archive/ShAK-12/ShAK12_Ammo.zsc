class HDB_ShAK_APEX:HDBulletActor{ //20x55mm APEX (Armour Piercing, EXplosive) 
	default{
		pushfactor 0.05;
		mass 150;
		speed 1000;
		accuracy 600;
		stamina 2000;
		woundhealth 5;
		hdbulletactor.hardness 2;
		hdbulletactor.distantsound "world/riflefar";
		hdbulletactor.distantsoundvol 2.;
	}
	override actor Puff(){
		if(max(abs(pos.x),abs(pos.y))>=32768)return null;
		setorigin(pos-(2*(cos(angle),sin(angle)),0),false);

		A_SprayDecal("BrontoScorch",16);
		if(vel==(0,0,0))A_ChangeVelocity(cos(pitch),0,-sin(pitch),CVF_RELATIVE|CVF_REPLACE);
		else vel*=0.01;
		if(tracer){ //warhead damage
			int dmg=random(60,80);

			//find the point at which it would pierce the middle
			vector3 hitpoint=pos+vel.unit()*tracer.radius;

			//find the "heart" point on the victim
			vector3 tracmid=(tracer.pos.xy,tracer.pos.z+tracer.height*0.618);

			dmg=int((1.-((hitpoint-tracmid).length()/tracer.radius))*dmg);
			tracer.damagemobj(
				self,target,
				dmg,
				"Piercing",DMG_THRUSTLESS
			);
		}
		doordestroyer.destroydoor(self,25,frandom(5,9),6,dedicated:false);
		A_HDBlast(
			fragradius:64,
			immolateradius:32,immolateamount:random(4,20),immolatechance:32,
			source:target
		);
		DistantQuaker.Quake(self,3,35,64,12);
		actor aaa=Spawn("WallChunker",pos,ALLOW_REPLACE);
		//A_SpawnChunks("HDSmoke",4,1,7);
		aaa=spawn("HDExplosion",pos,ALLOW_REPLACE);aaa.vel.z=2;
		aaa.scale.x=0.25;
		aaa.scale.y=0.25;
		//distantnoise.make(aaa,"world/rocketfar");
		A_SpawnChunks("HDSmokeChunk",random(3,4),6,12);

		bmissile=false;
		bnointeraction=true;
		vel=(0,0,0);
		if(!instatesequence(curstate,findstate("death")))setstatelabel("death");
		return null;
	}
	override void onhitactor(actor hitactor,vector3 hitpos,vector3 vu,int flags){
		double spbak=speed;
		super.onhitactor(hitactor,hitpos,vu,flags);
		if(spbak-speed>3)puff();
	}
	states{
	death:
		TNT1 A 0{if(tracer)puff();}
		goto super::death;
	}
}