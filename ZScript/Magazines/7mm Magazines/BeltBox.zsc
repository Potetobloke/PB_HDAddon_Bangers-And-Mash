class HD_PB_7mmBeltBox:HDMagAmmo{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "7.76mm LMG Box"
		//$Sprite "RMAGA0"

		hdmagammo.maxperunit 250;
		hdmagammo.roundtype "SevenMilAmmo";
		hdmagammo.roundbulk ENC_776_LOADED;
		hdmagammo.magbulk ENC_776BELTBOX_EMPTY;
		hdpickup.refid HDLD_776_BELTBOX;
		tag "7.76mm MG Box";
		inventory.pickupmessage "Picked up a 7.76mm MG belt box.";
		scale 0.8;
	}
	override string,string,name,double getmagsprite(int thismagamt){
		string magsprite=(thismagamt>0)?"MBX1A0":"MBX1B0";
		return magsprite,"RBRSA3A7","SevenMilAmmo",1.7;
	}
	
	override void DrawRoundCount(HDStatusBar sb,HDPlayerPawn hpl,name roundsprite,double scl,int offx,int offy){
		bool helptext=cvar.getcvar("hd_helptext",hpl.player).getbool();
		offx+=40;
		scl=1.6;
		sb.drawstring(
			sb.pSmallFont,sb.FormatNumber(hpl.countinv(roundtype)),
			(offx+2,offy),sb.DI_SCREEN_CENTER|sb.DI_TEXT_ALIGN_LEFT,
			font.CR_BROWN
		);
		sb.drawimage(roundsprite,(offx,offy),
			sb.DI_SCREEN_CENTER|sb.DI_ITEM_RIGHT_TOP,
			scale:(scl,scl)
		);
		
		sb.drawstring(
			sb.pSmallFont,sb.FormatNumber(hpl.countinv("HD_776BeltLink")),
			(offx+9,offy+9),sb.DI_SCREEN_CENTER|sb.DI_TEXT_ALIGN_LEFT,
			font.CR_BROWN
		);
		sb.drawimage("SBKZA0",(offx+7,offy+9),
			sb.DI_SCREEN_CENTER|sb.DI_ITEM_RIGHT_TOP,
			scale:(1.3,1.3)
		);
		
		sb.DrawString(
			sb.psmallfont,string.format("\cuDrop excess belt links\n with the \cqpickup manager\cu."),
			(offx-32,offy+20),sb.DI_SCREEN_CENTER|sb.DI_TEXT_ALIGN_LEFT,
			wrapwidth:smallfont.StringWidth("m")*80
		);
		
	}
	
	
	override void GetItemsThatUseThis(){
		itemsthatusethis.push("HD_M60");
		itemsthatusethis.push("HDMicrogun");
	}
	
	override bool Insert(){
		SyncAmount();
		int mindex=mags.size()-1;
		if(
			mags.size()<1
			||mags[mags.size()-1]>=maxperunit
			||!owner.countinv(roundtype)
			||!owner.countinv("HD_776BeltLink")
		)return false;
		owner.A_TakeInventory(roundtype,1,TIF_NOTAKEINFINITE);
		owner.A_TakeInventory("HD_776BeltLink",1,TIF_NOTAKEINFINITE);
		owner.A_StartSound("weapons/rifleclick2",CHAN_WEAPON);
		mags[mags.size()-1]++;
		
			inserttime=11;

		return true;
	}
	
	override bool Extract(){
		SyncAmount();
		if(
			mags.size()<1
			||mags[mags.size()-1]<1
		)return false;
		//Ammo Type 1
		if
		(HDPickup.MaxGive(owner,roundtype,roundbulk)>=1)
		{HDF.Give(owner,roundtype,1);}
		else 
		{HDPickup.DropItem(owner,roundtype,1);}
		//Ammo Type 2
		if(
		HDPickup.MaxGive(owner,"HD_776BeltLink",ENC_12MMBELTLINK)>=1)
		{HDF.Give(owner,"HD_776BeltLink",1);}
		else 
		{HDPickup.DropItem(owner,"HD_776BeltLink",1);}
		
		owner.A_StartSound("weapons/rifleclick2",8,CHANF_OVERLAP);
		mags[mags.size()-1]--;
		
		extracttime=2;
		
		return true;
	}
	
	states{
	spawn:
		MBX1 A -1;
		stop;
	spawnempty:
		MBX1 B -1{
			brollsprite=true;brollcenter=true;
			roll=randompick(0,0,0,0,2,2,2,2,1,3)*90;
		}stop;
	}
}

const ENC_776BELTLINK=0.005;

class HD_776BeltLink:HDRoundAmmo{
	default{
		+inventory.ignoreskill +forcexybillboard +cannotpush
		+hdpickup.multipickup
		+hdpickup.cheatnogive
		+rollsprite +rollcenter
		height 16;radius 8;
		tag "7.76mm belt link";
		hdpickup.refid HDLD_776BELTLINK;
		hdpickup.bulk ENC_776BELTLINK;
		xscale 0.7;yscale 0.7;
		inventory.pickupmessage "Picked up some belt links for 7.76mm.";
	}
	override void SplitPickup(){
		SplitPickupBoxableRound(30,300,"PB_776BeltLinkPile","SBKYA0","SBKZA0");
	}
	override void GetItemsThatUseThis(){
		itemsthatusethis.push("HD_M60");
		itemsthatusethis.push("HDMicrogun");
	}
	states{
	spawn:
		SBKZ A -1;
		SBKY A -1;
		stop;
	}
}

class PB_776BeltLinkPile:HDUPK{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Pile of 7.76 Belt Links"
		//$Sprite "BLT0B0"

		scale 0.7;
		hdupk.amount 300;
		hdupk.pickupsound "weapons/pocket";
		hdupk.pickupmessage "Picked up a pile of 7.76mm belt links.";
		hdupk.pickuptype "HD_776BeltLink";
	}
	states{
	spawn:
		SBKZ B -1;
	}
}

class HDFlyingBeltLink:HDSpent50AM{ //why does it inherit from the .50 AM casings? i dunno im lazy
	override void postbeginplay(){
		HDDebris.postbeginplay();
	}
	default{
		scale 0.7;
		bouncefactor 0.1;
		bouncesound "there is no sound";
	}
	states{
		spawn:
		SBKZ A 0 nodelay{
			A_SetRoll(roll+frandom(0,180));
		}
		SBKZ A 1{
			A_SetRoll(roll+45,SPF_INTERPOLATE);
		}wait;
	death:
		TNT1 A 1{
			actor a=spawn("HD_776BeltLink",self.pos,ALLOW_REPLACE);
			a.roll=self.roll;a.vel=self.vel;
		}stop;
	}
}
