//GRENADE

class HDWilliePeteGrenadeRoller:HDFragGrenadeRoller{
	default{
		scale 0.35;
		obituary "%o was roasted and toasted by %k.";
		radiusdamagefactor 0.04;pushfactor 1.4;maxstepheight 2;mass 35;
	}
	
	states{
	
	spawn:
		WPG1 A 0 nodelay{
			HDMobAI.Frighten(self,512);
		}
	spawn2:
		#### A 2{
			if(abs(vel.z-keeprolling.z)>10)A_StartSound("misc/fragknock",CHAN_BODY);
			else if(floorz>=pos.z)A_StartSound("misc/fragroll");
			keeprolling=vel;
			if(abs(vel.x)<0.4 && abs(vel.y)<0.4) setstatelabel("death");
		}loop;
	bounce:
		---- A 0{
			bmissile=false;
			vel*=0.3;
		}goto spawn2;
	death:
		---- A 2{
			if(abs(vel.z-keeprolling.z)>3){
				A_StartSound("misc/fragknock",CHAN_BODY);
				keeprolling=vel;
			}
			if(abs(vel.x)>0.4 || abs(vel.y)>0.4) setstatelabel("spawn");
		}wait;
	destroy:
		TNT1 A 3{
			bsolid=false;bpushable=false;bmissile=false;bnointeraction=true;bshootable=false;
			A_HDBlast(
				blastradius:256,blastdamage:115,fullblastradius:128,
				pushradius:256,pushamount:128,fullpushradius:96,
				fragradius:HDCONST_ONEMETRE*12,
				immolateradius:276,300,100
			);
			A_Immolate(tracer,target,90);
			DistantQuaker.Quake(self,4,35,512,10);
			A_StartSound("world/explode",CHAN_AUTO);
			A_AlertMonsters();
			actor xpl=spawn("WallChunker",self.pos-(0,0,1),ALLOW_REPLACE);
				xpl.target=target;xpl.master=master;xpl.stamina=stamina;
			xpl=spawn("HDExplosionWP",self.pos-(0,0,1),ALLOW_REPLACE);
				xpl.target=target;xpl.master=master;xpl.stamina=stamina;
				xpl.scale.x=2.3;xpl.scale.y=2.3;
			A_SpawnChunks("BigWallChunk",16,4,12);
			//A_SpawnChunks("HDWPSmokeChunk",21,6,20);
			//A_SpawnChunksFrags();
			distantnoise.make(self,"world/rocketfar");
		}
		TNT1 A 0
			{
				A_SpawnChunks("HDWPSmokeChunkA",4,12,15);
				A_SpawnChunks("HDWPSmokeChunkB",20,17,25);
			}
		stop;
	}
}


class HDWilliePeteGrenadeThrown:HDFragGrenade{

	int gasamount;
	
	default{
		+ROLLSPRITE
		+ROLLCENTER
		scale 0.35;
		obituary "%o was roasted and toasted.";
		hdfraggrenade.rollertype "HDWilliePeteGrenadeRoller";
		mass 35;
	}
	override void tick(){
		ClearInterpolation();
		if(isfrozen())return;
		if(!bmissile){
			hdactor.tick();return;
		}else if(fuze<140){
			fuze++;
			keeprolling=vel;
			slowprojectile.tick();
		}else{
			if(inthesky){
				distantnoise.make(self,"world/rocketfar");
				A_SpawnChunksFrags();
				destroy();return;
			}
			let gr=HDWilliePeteGrenadeRoller(spawn(rollertype,pos,ALLOW_REPLACE));
			gr.target=self.target;gr.master=self.master;gr.vel=self.vel;
			gr.fuze=fuze;
			destroy();return;
		}
	}
	states
	{
	spawn:
		WPG0 ABCDEFGH 2 A_SetRoll(frandom(-15.5, 15.5));
		loop;
		
	death:
		TNT1 A 2{
			bmissile=false;
			let wpg=HDWilliePeteGrenadeRoller(spawn(rollertype,self.pos,ALLOW_REPLACE));
			if(!wpg)return;
			wpg.target=self.target;wpg.master=self.master;
			wpg.fuze=self.fuze;
			wpg.vel=self.keeprolling;
			wpg.keeprolling=self.keeprolling;
			wpg.A_StartSound("misc/fragknock",CHAN_BODY);
			HDMobAI.Frighten(wpg,512);
		}stop;
	}
}

class HDWilliePeteGrenadeSpoon:HDDebris{
	default{
		scale 0.35;bouncefactor 0.6;
		bouncesound "misc/casing4";
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_StartSound("weapons/grenopen",CHAN_VOICE);
	}
	states{
	spawn:
		WPG0 X 2{roll+=40;}wait;
	death:
		WPG0 X -1;
	}
}

class HDWPSmokeChunkA:HDDebris{
	int burntime;
	int trailer;
	default{
		scale 0.3;
		damagetype "hot";
		obituary "%o was smoked and roasted.";
		bouncefactor 0.2;bouncesound "";
	}
	
	override void PostBeginPlay()
	{
		actor trl=spawn("HDSmokeWP",self.pos-(0,0,1),NO_REPLACE);
		actor smk=spawn("HDSmokeWP",self.pos-(0,0,1),NO_REPLACE);
			smk.A_ChangeVelocity(
					frandom(-2.9,2.9),frandom(-2.9,2.9),
					frandom(-2.9,2.9),
					CVF_RELATIVE
				);
		super.postbeginplay();
	}
	
	override void Tick(){
		if(isfrozen())return;
		super.tick();
		if(trailer>=70&&trailer<95)
		{
				A_ChangeVelocity(
					frandom(-2.9,2.9),frandom(-2.9,2.9),
					frandom(-0.9,0.9),
					CVF_RELATIVE
				);
		}
		if(trailer<=95){
		trailer+=random(1,3);
		actor trl=spawn("HDSmokeWP_Trail",self.pos-(0,0,1),ALLOW_REPLACE);
			}
		}
	states{
	spawn:
		TNT1 A random(3,7){
			if(trailer>=94)setstatelabel("death");
			A_StartSound("misc/firecrkl",CHAN_BODY,
				volume:0.2,pitch:frandom(0.8,1.2)
			);
		}
		wait;
	death:
		TNT1 A 0{
			//A_SetScale(randompick(-1,1)*frandom(0.4,0.6),frandom(0.4,0.6));
			//actor smk=spawn("HDSmokeWP",self.pos-(0,0,1),ALLOW_REPLACE);
			//smk.alpha=0.1;
			A_Immolate(self,self,945);
			A_StartSound("misc/firecrkl",CHAN_WEAPON,volume:0.8,pitch:frandom(0.8,1.2));
			trailer=96;
		}
		TNT1 A 1{scale*=1.1;}
	burn:
		TNT1 A 1 
		{
			A_Immolate(self,self,900);
			
			//a_spawnitemex("HDFlameRedBig",frandom(-1,1),frandom(-1,1),frandom(-1,1));	
			a_spawnitemex("HDSmokeWP_Trail",
						xvel:frandom(-0.5,0.5),
						yvel:frandom(-0.5,0.5),zvel:frandom(0.3,0.9));
			
			if(burntime<=951){
				burntime+=random(1,5);
				A_HDBlast(
				pushamount:0,
				immolateradius:48,25,74);}
			else if (burntime>=950){setstatelabel("stopburning");}
		}
		TNT1 AAAAA 1 
		{
			A_Immolate(self,self,25);
			
			if(burntime<=951){
				burntime+=random(1,5);
				A_HDBlast(
				pushamount:0,
				immolateradius:48,25,74);}
			else if (burntime>=950){destroy();}
			
		}
		loop;
		
	stopburning:
		TNT1 A 1;
		stop;
	}
}

class HDWPSmokeChunkB:HDWPSmokeChunkA
{
	default{}
	override void PostBeginPlay()
	{hddebris.postbeginplay();}
}

class HDSmokeWP:HDSmoke{
	default{
		scale 2.25;gravity 0.01;alpha 0.75;
		hdpuff.fadeafter 12;
		hdpuff.decel 0.96;
		hdpuff.fade 0.99;
		hdpuff.grow 0.01;
		hdpuff.minalpha 0.001;
		hdpuff.startvelz 2.;
		+rollsprite
	}
	override void postbeginplay()
		{A_SetRoll(roll+frandom(0,360));hdactor.postbeginplay();}
	states{
	spawn:
		WPS0 ABCD 0{frame=random(0,3);}
		WPS0 ### 1 A_SetScale(scale.y*1.25);
		WPS0 ## 1 A_SetScale(scale.y*1.15);
		WPS0 # 1 A_SetScale(scale.y*1.05);
		WPS0 # -1;
		wait;
	}
}

class HDSmokeWP_Trail:HDSmokeWP{
	default{
		scale 0.05;gravity 0.06;alpha 0.5;
		hdpuff.fadeafter 6;
		hdpuff.decel 0.99;
		hdpuff.fade 0.97;
		hdpuff.grow 0.02;
		hdpuff.minalpha 0.005;
	}
	states{
	spawn:
		WPS0 A 0;WPS0 A 0 A_SetScale(scale.y*2);
		WPS0 ABCD -1{frame=random(0,3);}wait;
	}
}

class HDExplosionWP:IdleDummy{
	default{
		+forcexybillboard +bright
		alpha 1.0;renderstyle "add";
		deathsound "world/explode";
	}
	states{
	spawn:
	death:
		MISL B 0 nodelay{
			if(max(abs(pos.x),abs(pos.y),abs(pos.z))>=32768){destroy();return;}
			vel.z+=2;
			A_StartSound(deathsound,CHAN_BODY);
			let xxx=spawn("HDExplosionLight",pos);
			xxx.target=self;
		}
		MISL BB 0 A_SpawnItemEx("ParticleWhiteSmall", 0,0,0, vel.x+random(-2,2),vel.y+random(-2,2),vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		//MISL BBBBBB 0 A_SpawnItemEx("HDSmokeWP", 0,0,0, vel.x+frandom(-2,2),vel.y+frandom(-2,2),vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION);
		MISL B 0 A_Jump(256,"fade");
	extreme:
		EXP3 ABCDEF 1 BRIGHT {A_HDBlast(pushamount:0,immolateradius:128,25,74);}
		stop;
	}
}