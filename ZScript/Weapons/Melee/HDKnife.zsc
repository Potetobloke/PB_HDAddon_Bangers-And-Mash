// ------------------------------------------------------------
// Combat Knife
// ------------------------------------------------------------
class HDStabber:IdleDummy{
	default{
		+bloodlessimpact +nodecal +hittracer +puffonactors
		stamina 1;
	}
}

const HDLD_KNIFE="knf";

class HDCombatKnife:HD_PB_UnarmedWeapon{
	int targettimer;
	int targethealth;
	int targetspawnhealth;
	bool flicked;
	bool washolding;
	
		double initangleto;
	double inittangle;
	double inittz;
	vector3 initpos;
	vector3 initvel;
	
	default{
		+WEAPON.MELEEWEAPON +WEAPON.NOALERT +WEAPON.NO_AUTO_SWITCH
		+hdweapon.fitsinbackpack
		//-hdweapon.dontnull
		-weapon.cheatnotweapon
		-nointeraction
		-inventory.untossable
		+forcepain
		+forceXYbillboard
		+rollsprite
		+rollcenter
		+hdweapon.reverseguninertia
		+nodamagethrust
		radius 3;height 3;
		inventory.pickupmessage "You got the combat knife!";
		hdweapon.barrelsize 1,1,1;
		obituary "%o got shanked by %k.";
		attacksound "*fist";
		weapon.selectionorder 100;
		weapon.kickback 15;
		weapon.bobstyle "Alpha";
		weapon.bobspeed 2.6;
		weapon.bobrangex 0.1;
		weapon.bobrangey 0.5;
		weapon.slotnumber 1;
		weapon.slotpriority 2.1;
		scale 0.6;
		//+FLATSPRITE
		DamageFunction (random(30,40));
		tag "Combat Knife";
		hdweapon.refid HDLD_KNIFE;
	}
	override bool AddSpareWeapon(actor newowner){return AddSpareWeaponRegular(newowner);}
	override hdweapon GetSpareWeapon(actor newowner,bool reverse,bool doselect){return GetSpareWeaponRegular(newowner,reverse);}
	
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		let ww=HDCombatKnife(hdw);
		if(ww.targethealth)sb.drawwepnum(ww.targethealth,ww.targetspawnhealth);
	}
	override string gethelptext(){
		return
		WEPHELP_FIRE.."  Stab\n"
		..WEPHELP_ALTFIRE.."   Lunge\n"
		..WEPHELP_RELOAD.."   Distracting strike\n"
		..WEPHELP_FIREMODE.."   Prepare throw\n"
		..WEPHELP_RELOAD.."+"..WEPHELP_FIRE.."   Rapid Stabs\n"
		//..WEPHELP_UNLOAD.."   Distracting projectile\n"
		;
	}
	
	override string,double getpickupsprite(){return "KNFPZ0",0.35;}
	
	override double weaponbulk(){
		return 15;
	}
	override double gunmass(){
		return 3;
	}
	
	action void A_Lunge(){
		hdplayerpawn hdp=hdplayerpawn(self);
		if(hdp){
			if(hdp.fatigue>=30){setweaponstate("hold");return;}
			else hdp.fatigue+=3;
		}
		A_Recoil(min(hdp.overloaded*0.6,4.)-4.);
	}
	
	//for throwing a weapon
	override inventory CreateTossable(int amt){
		let onr=hdplayerpawn(owner);
		bool throw=(
			onr
			&&onr.player
			&&onr.player.cmd.buttons&BT_ZOOM
		);
		bool isreadyweapon=onr&&onr.player&&onr.player.readyweapon==self;
		if(!isreadyweapon)throw=false;
		let thrown=super.createtossable(amt);
		if(!thrown)return null;
		let newwep=GetSpareWeapon(onr,doselect:isreadyweapon);
		hdweapon(thrown).bjustchucked=true;
		thrown.target=onr;
		thrown.lastenemy=onr;
		if(throw){
			thrown.bmissile=true;
			//a_setangle(-90+angle);
			thrown.bFLATSPRITE=true;
			thrown.bNOEXTREMEDEATH=true;
			thrown.bBOUNCEAUTOOFF=true;
		}else{
			thrown.bmissile=false;
			thrown.bFLATSPRITE=false;
			thrown.bNOEXTREMEDEATH=false;
			thrown.bBOUNCEAUTOOFF=false;
			}
		return thrown;
	}
	//an override is needed because DropInventory will undo anything done in CreateTossable
	double throwvel;
	override void OnDrop(Actor dropper){
		if(bjustchucked&&target){
			double cp=cos(target.pitch);
			if(bmissile){
				vel=target.vel+
					(cp*(cos(target.angle),sin(target.angle)),-sin(target.pitch))
					*min(20,800/weaponbulk())
					*(hdplayerpawn(target)?hdplayerpawn(target).strength:1.)
				;
			}else vel=target.vel+(cp*(cos(target.angle),sin(target.angle)),-sin(target.pitch))*4;
			throwvel=vel dot vel;
			bjustchucked=false;
		}

		//copypasted from HDPickup
		if(dropper){
			setz(dropper.pos.z+dropper.height*0.8);
			if(!bmissile){
				double dp=max(dropper.pitch-6,-90);
				vel=dropper.vel+(
					cos(dp)*(cos(dropper.angle),sin(dropper.angle)),
					-sin(dp)
				)*3;
			}
			HDBackpack.ForceUpdate(dropper);
		}
	}

	override void DoEffect(){
		super.DoEffect();
		if(targettimer<70)targettimer++;else{
			tracer=null;
			targettimer=0;
			targethealth=0;
		}
	}
	action void HDStab(double dmg){
		flinetracedata punchline;
		
		A_StartSound("misc/fwoosh",CHAN_WEAPON,CHANF_OVERLAP,volume:0.024,pitch:1.5);
		
		bool punchy=linetrace(
			angle,54,pitch,
			TRF_NOSKY,
			offsetz:height-12,
			data:punchline
		);
		if(!punchy)return;

		//actual puff effect if the shot connects
		LineAttack(angle,54,pitch,punchline.hitline?(countinv("HDZerk")?random(50,120):random(5,15)):0,"none",
			countinv("HDZerk")?"BulletPuffMedium":"BulletPuffSmall",
			flags:LAF_NORANDOMPUFFZ|LAF_OVERRIDEZ,
			offsetz:height-12
		);

		if(!punchline.hitactor){
			HDF.Give(self,"WallChunkAmmo",1);
			return;
		}
		actor punchee=punchline.hitactor;

		//charge!
		if(invoker.flicked)dmg*=1.5;
		else dmg+=HDMath.TowardsEachOther(self,punchee)*2;

		//come in swinging
		let onr=hdplayerpawn(self);
		if(onr){
			int iy=max(abs(player.cmd.pitch),abs(player.cmd.yaw));
			if(iy>0)iy*=100;
			else if(iy<0)iy*=200;
			dmg+=min(abs(iy),dmg*2);

			//need to be well grounded
			if(floorz<pos.z)dmg*=0.5;
		}

		//shit happens
		HDBleedingWound.inflict(punchee,int(dmg*=invoker.strength*frandom(1.2,1.6)),frandom(5,8));

		//other effects
		if(
			onr
			&&!punchee.bdontthrust
			&&(
				punchee.mass<200
				||(
					punchee.radius*2<punchee.height
					&& punchline.hitlocation.z>punchee.pos.z+punchee.height*0.6
				)
			)
		){
			double iyaw=player.cmd.yaw*(65535./360.);
			if(abs(iyaw)>(0.5)){
				punchee.A_SetAngle(punchee.angle-iyaw*100,SPF_INTERPOLATE);
			}
			double ipitch=player.cmd.pitch*(65535./360.);
			if(abs(ipitch)>(0.5*65535/360)){
				punchee.A_SetPitch(punchee.angle+ipitch*100,SPF_INTERPOLATE);
			}
		}
		//headshot lol
		if(
			!punchee.bnopain
			&& punchee.health>0
			&& !(punchee is "HDBarrel")
			&& punchee.findstate("pain")
			&& punchline.hitlocation.z>punchee.pos.z+punchee.height*0.75
		){
			if(hd_debug)A_Log("HEAD SHOT");
			punchee.setstatelabel("pain");
			dmg*=frandom(2.1,2.3); 
			HDBleedingWound.inflict(punchee,5,frandom(5,8),true);
			//i am very sure getting stabbed in the head would be very fatal
		}

		if(hd_debug){
			string pch="";
			if(punchee.player)pch=punchee.player.getusername();
				else pch=punchee.getclassname();
			A_Log(string.format("Punched %s for %i damage!",pch,dmg));
		}
		punchee.A_StartSound("weapons/kharonflesh",CHAN_7,volume:0.6);
		if(punchee.health>0)punchee.damagemobj(self,self,int(dmg),"slashing");
		//else HDF.Give(punchee,"SawGib",dmg*0.2);

		if(!punchee)invoker.targethealth=0;else{
			invoker.targethealth=punchee.health;
			invoker.targetspawnhealth=punchee.spawnhealth();
			invoker.targettimer=0;
		}
	}

	states{
	
	select:
		TNT1 A 0{
			//these two don't actually work???
			A_OverlayFlags(PSP_WEAPON,PSPF_CVARFAST|PSPF_POWDOUBLE,false);
			A_OverlayFlags(PSP_FLASH,PSPF_CVARFAST|PSPF_POWDOUBLE,false);

			A_WeaponBusy();
			A_SetCrosshair(21);
			A_SetHelpText();
			
			invoker.weaponstatus[FRAGS_FORCE]=0;

			return resolvestate("select0");
		}
		select0:
		CKF0 E 0;
		goto select0small;
	deselect:
		TNT1 A 0 A_StartDeselect();
	deselect0:
		CKF0 E 0;
		goto deselect0small;
		
	ready:
		CKF0 E 1{
			if(invoker.washolding&&pressingfire()){
				setweaponstate("nope");
				return;
			}
			A_WeaponReady(WRF_ALL);
			invoker.flicked=false;
			invoker.washolding=false;
		}goto readyend;
	reload:
	flick:
		CKF0 A 1 offset(0,50) A_Lunge();
		#### A 1 offset(0,36);
		#### A 0 A_JumpIfInventory("HDZerk",1,"ZerkFlick");
		#### AAAAAAA 0 A_CustomPunch((1),1,CPF_PULLIN,"HDFistPuncher",36);
		goto flickend;
	zerkflick:
		#### AAAAAAA 0 A_CustomPunch((random(1,3)),1,CPF_PULLIN,"HDFistPuncher",36);
	flickend:
		#### AA 1 offset(0,38){invoker.flicked=true;}
		#### A 1 offset(0,42);
		#### A 1 offset(0,50);
		#### A 0 A_JumpIf(pressingfire(),"superstab");
		goto fire;
	fire:
	hold:
	althold:
	startfire:
		CKF0 A 0 A_JumpIfInventory("HDZerk",1,"zerkpunch");
		goto punch;
	punch:
		CKF0 C 1 offset(0,32) A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		#### D 3 offset(6,28) HDStab(12);
		#### D 3 offset(0,32) A_WeaponOffset(frandom(-3,3),frandom(30,34));
		#### CB 3;
		#### E 3;
		#### E 0 A_JumpIf(pressingaltfire(),"altfire");
		#### E 1 A_ReFire();
		goto ready;
	zerkpunch:
		CKF0 D 0 {A_Recoil(-1); A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);}
		#### D 0 HDStab(invoker.flicked?150:110);
		#### D 3 A_WeaponOffset(frandom(-12,12),frandom(30,34));
		#### CBE 1;
		#### E 1;
		#### E 0 A_JumpIf(pressingaltfire(),"altfire");
		#### E 2 A_ReFire();
		goto ready;
	altfire:
		CKF0 E 1 offset(0,36);
		#### E 1 offset(0,50);
		#### E 2 A_CheckFloor("lunge");
		goto nope;
	lunge:
		TNT1 A 0 A_Lunge();
		TNT1 AA 1{
			if(countinv("HDZerk"))A_Recoil(-random(12,24));
		}
		TNT1 A 1 A_Recoil(-4);
	goto startfire;
	grabhold:
		CKF0 A 1 {invoker.weaponstatus[FRAGS_FORCE]=50;}
		TNT1 A 0 A_JumpIf(pressingfire(),"YEET");
		TNT1 A 0 A_JumpIf(pressingfiremode(),"grabhold");
		goto nope;
	firemode:
	grab:
	grab2:
		CKF0 A 1 offset(0,52);
		#### A 1 offset(0,32);
		goto grabhold;
	YEET:
		---- A 1
		{
		if(player&&hdweapon(player.readyweapon)){
		 player.cmd.buttons|=BT_ZOOM;
		 DropInventory(player.readyweapon);
		}
		}
		TNT1 A 0;
		goto nope;
	spawn:
		KNFP X 0 A_JumpIf(invoker.bNOEXTREMEDEATH==true,"spawn2");
		KNFP Y 1;
		loop;
	spawn2:
		KNFP X 1 A_SetPitch(15+pitch);
		loop;
	spawn3:
		KNFP X 1;
		//	{setorigin((pos+tracer.pos)*0.5,true);}
		loop;
	
	superstab:
		CKF0 A 0 A_JumpIfInventory("HDZerk",1,"zerksuperstab");
		CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		#### D 0 {HDStab(6);hdplayerpawn(self).fatigue+=1;}
		#### D 2 offset(-3,34);//A_WeaponOffset(frandom(-3,3),frandom(30,34));
		#### CB 1;
		#### E 3;
		#### E 1 A_ReFire("superstab2");
		goto fire;
	superstab2:
		CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		#### D 0 {HDStab(6);hdplayerpawn(self).fatigue+=1;}
		#### D 2 offset(6,30);//A_WeaponOffset(frandom(-6,6),frandom(30,34));
		#### CB 1;
		#### E 3;
		#### E 1 A_ReFire("superstab3");
		goto fire;
	superstab3:
		CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		#### D 0 {HDStab(6);hdplayerpawn(self).fatigue+=2;}
		#### D 3 offset(4,32);//A_WeaponOffset(frandom(-8,8),frandom(30,34));
		#### CB 1;
		#### E 3;
		#### E 1 A_ReFire("superstab4");
		goto fire;
	superstab4:
		CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		#### D 0 {HDStab(6);hdplayerpawn(self).fatigue+=2;}
		#### D 5 offset(3,31);//A_WeaponOffset(frandom(-12,12),frandom(30,34));
		#### CB 1;
		#### E 3;
		#### E 1 A_ReFire("superstab5");
		goto fire;
	superstab5:
		CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		#### D 0 {HDStab(25);hdplayerpawn(self).fatigue+=3;}
		#### D 9 offset(1,32);//A_WeaponOffset(frandom(-7,7),frandom(30,34));
		#### CB 3;
		#### E 3;
		goto fire;
	zerksuperstab:
		CKF0 D 0 {A_Recoil(-1); A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);}
		#### D 0 {HDStab(invoker.flicked?150:110);hdplayerpawn(self).fatigue+=4;}
		#### D 2 A_WeaponOffset(frandom(-12,12),frandom(30,34));
		#### CBE 1;
		#### E 1;
		//#### E 0 A_JumpIf(pressingaltfire(),"altfire");
		#### E 1 A_ReFire("zerksuperstab");
		goto ready;
	
	death:
		KNFP X 1 {
		invoker.bjustchucked=false;
		A_SpawnItemEx("BulletPuffSmall");
		A_NoGravity();
			if(tracer){
			
				let hdt=hdmobbase(tracer);
				
				if(
					pos.z-tracer.pos.z>tracer.height*0.8
					&&(
						!hdt
						||(
							!hdt.bnovitalshots
							&&!hdt.bheadless
						)
					)
				){
					if(hd_debug)A_Log("HEAD SHOT");
					bpiercearmor=true;
					damagemobj(tracer,self,5,"slashing");
				}
				
				if(tracer.bnoblood)
				{
				HDBleedingWound.inflict(tracer,15);
				}
				else 
				{
				HDBleedingWound.inflict(tracer,15);
				}
			}
		}
		goto spawn3;
	Xdeath:
		KNFP X 1 {
		invoker.bjustchucked=false;
			if(tracer){
			
				let hdt=hdmobbase(tracer);
				
				if(
					pos.z-tracer.pos.z>tracer.height*0.8
					&&(
						!hdt
						||(
							!hdt.bnovitalshots
							&&!hdt.bheadless
						)
					)
				){
					if(hd_debug)A_Log("HEAD SHOT");
					bpiercearmor=true;
					damagemobj(tracer,self,75,"slashing");
				}
				
				if(tracer.bnoblood)
				{
				HDBleedingWound.inflict(tracer,15);
				}
				else 
				{
				HDBleedingWound.inflict(tracer,15);
				}
			}
		}
		goto spawn3;
	Crash:
		KNFP X 1 {
		invoker.bjustchucked=false;
			if(tracer){
			
				let hdt=hdmobbase(tracer);
				
				if(
					pos.z-tracer.pos.z>tracer.height*0.8
					&&(
						!hdt
						||(
							!hdt.bnovitalshots
							&&!hdt.bheadless
						)
					)
				){
					if(hd_debug)A_Log("HEAD SHOT");
					bpiercearmor=true;
					damagemobj(tracer,self,75,"slashing");
				}
			
			
				if(tracer.bnoblood)
				{
				HDBleedingWound.inflict(tracer,35,7);
				}
				else 
				{
				HDBleedingWound.inflict(tracer,35,7);
				A_StartSound("weapons/kharonflesh",CHAN_7,volume:0.6);
				}
			}
		}
		goto spawn3;
	unload:
		#### E 0;
		goto nope;
	ohnomyknife:
		tnt1 a 0;
		stop;
	}
}
