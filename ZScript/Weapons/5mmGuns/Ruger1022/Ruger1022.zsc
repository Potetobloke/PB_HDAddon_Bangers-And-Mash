
const HDLD_RUGER1022="r2g";
const HDLD_RUGER1022_MAG="r2m";

const ENC_RUGER1022_MAG_EMPTY=4.25;
const ENC_RUGER1022_MAG_LOADED=0.2;

const ENC_RUGER1022_EXMAG_EMPTY=10.1;
const ENC_RUGER1022_EXMAG_LOADED=4.11;

// ------------------------------------------------------------
// Buger M22 Magazine
// ------------------------------------------------------------

class HDRuger1022Mag:HDMagAmmo{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "AutoMag Magazine"
		//$Sprite "50AMA0"

		hdmagammo.maxperunit 10;
		hdmagammo.roundtype "HD5MM_Ammo";
		hdmagammo.roundbulk ENC_5MM_LOADED;
		hdmagammo.magbulk ENC_RUGER1022_MAG_EMPTY;
		tag "Buger M22 Magazine";
		inventory.pickupmessage "Picked up a small target rifle magazine.";
		hdpickup.refid HDLD_RUGER1022_MAG;
	}
	override string,string,name,double getmagsprite(int thismagamt){
		string magsprite=(thismagamt>0)?"R10MA0":"R10MB0";
		return magsprite,"5MMRY0","HD5MM_Ammo",2.;
	}
	override void GetItemsThatUseThis(){
		itemsthatusethis.push("HD_Ruger1022");
	}
	states{
	spawn:
		R10M A -1;
		stop;
	spawnempty:
		R10M B -1{
			brollsprite=true;brollcenter=true;
			roll=randompick(0,0,0,0,2,2,2,2,1,3)*90;
		}stop;
	}
}

// ------------------------------------------------------------
// Buger M22 Target Rifle
// ------------------------------------------------------------

class HD_Ruger1022:HDWeapon{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "Buger M22 Target Rifle"
		//$Sprite "G11PA0"

		+hdweapon.fitsinbackpack
		obituary "%o died from lead poisoning. Thanks, %k.";
		weapon.selectionorder 18;
		weapon.slotnumber 4;
		scale 0.7;
		weapon.bobrangex 0.22;
		weapon.bobrangey 0.9;
		inventory.pickupmessage "You got the target rifle! Hunt some small game with it!";
		hdweapon.barrelsize 26,1,2.25;
		hdweapon.refid HDLD_RUGER1022;
		tag "Buger M22 Target Rifle";
	}
	override bool AddSpareWeapon(actor newowner){return AddSpareWeaponRegular(newowner);}
	override hdweapon GetSpareWeapon(actor newowner,bool reverse){return GetSpareWeaponRegular(newowner,reverse);}
	override double gunmass(){
		return 6.6+(weaponstatus[RG12S_MAG]<0)?-0.5:(weaponstatus[RG12S_MAG]*0.02);
	}
	override double weaponbulk(){
		return 95;
	}
	override void failedpickupunload(){
		failedpickupunloadmag(RG12S_MAG,"HDRuger1022Mag");
	}
	override void DropOneAmmo(int amt){
		if(owner){
			amt=clamp(amt,1,10);
			if(owner.countinv("HD5MM_Ammo"))owner.A_DropInventory("HD5MM_Ammo",amt*10);
			else owner.A_DropInventory("HDRuger1022Mag",amt);
		}
	}
	override string,double getpickupsprite(){
		return "R10P"..((weaponstatus[RG12S_MAG]<0)?"W":"W").."0";
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.hudlevel==1){
			int nextmagloaded=sb.GetNextLoadMag(hdmagammo(hpl.findinventory("HDRuger1022Mag")));
			if(nextmagloaded>=10){
				sb.drawimage("R10MNORM",(-48,-3),sb.DI_SCREEN_CENTER_BOTTOM);
			}else if(nextmagloaded<1){
				sb.drawimage("R10MMPTY",(-48,-3),sb.DI_SCREEN_CENTER_BOTTOM,alpha:nextmagloaded?0.6:1.);
			}else sb.drawbar(
				"R10MNORM","R10MGREY",
				nextmagloaded,10,
				(-48,-3),-1,
				sb.SHADER_VERT,sb.DI_SCREEN_CENTER_BOTTOM
			);
			sb.drawnum(hpl.countinv("HDRuger1022Mag"),-43,-8,sb.DI_SCREEN_CENTER_BOTTOM);
		}
		sb.drawwepcounter(hdw.weaponstatus[RG12S_AUTO],
			-22,-10,"RBRSA3A7","STFULAUT","STBURAUT"
		);
		sb.drawwepnum(hdw.weaponstatus[RG12S_MAG],10);
		if(hdw.weaponstatus[RG12S_CHAMBER]==2)sb.drawwepdot(-16,-10,(3,1));
	}
	override string gethelptext(){
		return
		WEPHELP_FIRESHOOT
		..WEPHELP_RELOAD.."  Reload\n"
		..WEPHELP_FIREMODE.."  Semi/Burst\n"
		..WEPHELP_MAGMANAGER
		..WEPHELP_UNLOADUNLOAD
		;
	}
	override void DrawSightPicture(
		HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl,
		bool sightbob,vector2 bob,double fov,bool scopeview,actor hpc,string whichdot
	){
		int cx,cy,cw,ch;
		[cx,cy,cw,ch]=screen.GetClipRect();
		sb.SetClipRect(
			-16+bob.x,-4+bob.y,32,16,
			sb.DI_SCREEN_CENTER
		);
		vector2 bobb=bob*3;
		bobb.y=clamp(bobb.y,-8,8);
		sb.drawimage(
			"frntsite",(0,0)+bobb,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			alpha:0.9
		);
		sb.SetClipRect(cx,cy,cw,ch);
		sb.drawimage(
			"backsite",(0,0)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP
		);
	}
	states{
	select0:
	TNT1 A 0;
		R10G A 0;
		#### # 0;
		goto select0big;
	deselect0:
		R10G A 0;
		#### # 0;
		goto deselect0big;

	ready:
		R10G A 0;
		#### # 1{
			A_SetCrosshair(21);
			A_WeaponReady(WRF_ALL);
			invoker.weaponstatus[RG12S_RATCHET]=0;
			}
		goto readyend;
	user3:
		---- A 0 A_MagManager("HDRuger1022Mag");
		goto ready;
	altfire:
	althold:
		goto nope;
	hold:
		R10G A 0{
			if(
				//full auto
				invoker.weaponstatus[RG12S_AUTO]==2
			)setweaponstate("fire2");
			else if(
				//burst
				invoker.weaponstatus[RG12S_AUTO]<1
				//||invoker.weaponstatus[RG12S_RATCHET]>4
			)setweaponstate("nope");
		}goto fire2;
	user2:
	firemode:
		---- # 1{
			int aut=invoker.weaponstatus[RG12S_AUTO];
			if(aut>=1)invoker.weaponstatus[RG12S_AUTO]=0;
			else if(aut<0)invoker.weaponstatus[RG12S_AUTO]=0;
			else invoker.weaponstatus[RG12S_AUTO]++;
		}goto nope;
	fire:
		R10G # 1;
	fire2:
		R10G A 0{
			if(invoker.weaponstatus[RG12S_CHAMBER]==2){
				A_GunFlash();
			}else{
				if(invoker.weaponstatus[RG12S_MAG]>0)setweaponstate("chamber");
				else setweaponstate("nope");
			}
		}
		R10G B 1;
		R10G C 1;
		#### C 0{
			if(invoker.weaponstatus[RG12S_CHAMBER]==1){
				A_SpawnItemEx("HDSpent5mmMR",
					cos(pitch)*10,0,height-10-sin(pitch)*10,
					vel.x,vel.y,vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
				invoker.weaponstatus[RG12S_CHAMBER]=0;
			}
			if(invoker.weaponstatus[RG12S_MAG]>0){
				invoker.weaponstatus[RG12S_MAG]--;
				invoker.weaponstatus[RG12S_CHAMBER]=2;
			}
			if(invoker.weaponstatus[RG12S_AUTO]==1)
			{
			invoker.weaponstatus[RG12S_RATCHET]++;
			A_ReFire();
			}
		}
		goto nope;
	
	flash:
		R10F A 1 bright{
			A_Light1();
			HDFlashAlpha(-16);
			A_StartSound("weapons/ruger1022/fire",CHAN_WEAPON);

			//shoot the bullet
			//copypaste any changes to spawnshoot as well!
			HDBulletActor.FireBullet(self,"HDBullet5mmMR");
			invoker.weaponstatus[RG12S_CHAMBER]=1;
			A_MuzzleClimb(-frandom(-0.1,0.2),-frandom(0.1,0.3),-frandom(-0.1,0.3),-frandom(0.1,0.2));
			A_AlertMonsters();
		}
		goto lightdone;


	unloadchamber:
		R10G G 2;
		#### H 2 offset(2,34) A_JumpIf(invoker.weaponstatus[RG12S_CHAMBER]<1,"nope");
		#### I 2 offset(5,40) A_StartSound("weapons/ruger1022/cock",CHAN_6,0,0.5);
		#### J 3 offset(9,47){
			class<actor>which=invoker.weaponstatus[RG12S_CHAMBER]>1?"HD5MM_Ammo":"HDSpent9mm";
			invoker.weaponstatus[RG12S_CHAMBER]=0;
			A_SpawnItemEx(which,
				cos(pitch)*10,0,height-8-sin(pitch)*10,
				vel.x,vel.y,vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
			);
		}
		#### I 2 offset(5,37);
		#### H 2 offset(2,36);
		goto readyend;
	loadchamber:
		---- A 0 A_JumpIf(invoker.weaponstatus[RG12S_CHAMBER]>0,"nope");
		---- A 0 A_JumpIf(!countinv("HD5MM_Ammo"),"nope");
		---- A 1 offset(0,34) A_StartSound("weapons/pocket",CHAN_WEAPON);
		---- A 1 offset(2,36);
		#### A 1 offset(5,40);
		#### A 4 offset(4,39){
			if(countinv("HD5MM_Ammo")){
				A_TakeInventory("HD5MM_Ammo",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[RG12S_CHAMBER]=2;
				A_StartSound("weapons/ruger1022/fwd",CHAN_WEAPON);
			}
		}
		#### A 7 offset(5,37);
		#### A 1 offset(2,36);
		#### A 1 offset(0,34);
		goto readyend;
	user4:
	unload:
		R10G A 0;
		#### A 0{
			invoker.weaponstatus[0]|=RG12F_JUSTUNLOAD;
			if(
				invoker.weaponstatus[RG12S_MAG]>=0
			)setweaponstate("unmag");
			else if(invoker.weaponstatus[RG12S_CHAMBER]>0)setweaponstate("unloadchamber");
		}goto nope;
	reload:
		R10G A 0;
		#### A 0{
			invoker.weaponstatus[0]&=~RG12F_JUSTUNLOAD;
			if(invoker.weaponstatus[RG12S_MAG]>=25)setweaponstate("nope");
			else if(HDMagAmmo.NothingLoaded(self,"HDRuger1022Mag")){
				if(
					invoker.weaponstatus[RG12S_MAG]<0
					&&countinv("HD5MM_Ammo")
				)setweaponstate("loadchamber");
				else setweaponstate("nope");
			}
		}goto unmag;
	unmag:
		R10G A 0;
		#### A 1 offset(0,34) A_SetCrosshair(21);
		#### A 1 offset(5,38);
		#### D 1 offset(10,42);
		#### E 2 offset(20,46) A_StartSound("weapons/smgmagclick",CHAN_WEAPON);
		#### F 4 offset(30,52){
			A_MuzzleClimb(0.3,0.4);
			A_StartSound("weapons/smgmagmove",CHAN_WEAPON);
		}
		#### E 0{
			int magamt=invoker.weaponstatus[RG12S_MAG];
			if(magamt<0){
				setweaponstate("magout");
				return;
			}
			invoker.weaponstatus[RG12S_MAG]=-1;
			if(
				(!PressingUnload()&&!PressingReload())
				||A_JumpIfInventory("HDRuger1022Mag",0,"null")
			){
				HDMagAmmo.SpawnMag(self,"HDRuger1022Mag",magamt);
				setweaponstate("magout");
			}else{
				HDMagAmmo.GiveMag(self,"HDRuger1022Mag",magamt);
				A_StartSound("weapons/pocket",CHAN_WEAPON);
				setweaponstate("pocketmag");
			}
		}
	pocketmag:
		#### FF 7 offset(34,54) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
	magout:
		#### F 0{
			if(invoker.weaponstatus[0]&RG12F_JUSTUNLOAD)setweaponstate("reloadend");
			else setweaponstate("loadmag");
		}

	loadmag:
		#### F 0 A_StartSound("weapons/pocket",CHAN_WEAPON);
		#### F 6 offset(34,54) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		#### F 7 offset(34,52) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		#### F 10 offset(32,50);
		#### F 3 offset(32,49){
			let mmm=hdmagammo(findinventory("HDRuger1022Mag"));
			if(mmm){
				invoker.weaponstatus[RG12S_MAG]=mmm.TakeMag(true);
				A_StartSound("weapons/smgmagclick",CHAN_BODY);
			}
			if(
				invoker.weaponstatus[RG12S_MAG]<1
				||invoker.weaponstatus[RG12S_CHAMBER]>0
			)setweaponstate("reloadend");
		}
	chamber:
		R10G A 0;
		R10G DGH 2;
		R10G I 3 A_StartSound("weapons/ruger1022/cock",CHAN_WEAPON);
		#### J 4 offset(33,50){
			invoker.weaponstatus[RG12S_MAG]--;
			invoker.weaponstatus[RG12S_CHAMBER]=2;
		}
		#### IHG 2 offset(32,49);

	reloadend:
		#### G 3 offset(30,52);
		#### A 2 offset(20,46);
		#### A 1 offset(10,42);
		#### A 1 offset(5,38);
		#### A 1 offset(0,34);
		goto nope;



	spawn:
		R10P W -1 nodelay;//{
			//if(invoker.weaponstatus[RG12S_MAG]<0)frame=1;
		//}
	}
	override void initializewepstats(bool idfa){
		weaponspecial = 1337;
		weaponstatus[0]=0;
		weaponstatus[RG12S_MAG]=10;
		weaponstatus[RG12S_CHAMBER]=2;
		if(!idfa)weaponstatus[RG12S_AUTO]=0;
	}
}

enum ruger1022status{
	RG12F_JUSTUNLOAD=1,

	RG12S_FLAGS=0,
	RG12S_MAG=1,
	RG12S_CHAMBER=2, //0 empty, 1 spent, 2 loaded
	RG12S_AUTO=3, //0 semi, 1 burst, 2 auto
	RG12S_RATCHET=4,
};
